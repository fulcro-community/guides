= Advanced Minimalist Fulcro Tutorial
:toc:
:toc-placement!:
:toclevels: 2
:description: A follow-up to the Minimalist Fulcro Tutorial that focuses on a few non-essential yet often necessary topics in the traditional minimalist way.

:url-book: https://book.fulcrologic.com/
:url-code-rad: https://github.com/fulcrologic/fulcro-rad/blob/fulcro-rad-1.0.23/src/main/com/fulcrologic/rad/
:url-code-rad-demo: https://github.com/fulcrologic/fulcro-rad-demo/tree/de4a5402b3526687b6debe41296d9a6f45648c20/
:url-cljdoc-fulcro: https://cljdoc.org/d/com.fulcrologic/fulcro/3.5.6/api/

Author: https://holyjak.cz/[Jakub Hol√Ω] & contributors

A follow-up to the Minimalist Fulcro Tutorial that focuses on a few non-essential yet often necessary topics in the traditional minimalist way.

WARNING: This is work in progress at an early stage. You can https://github.com/fulcro-community/guides/issues/8[subscribe to this issue] to get notified of significant updates.

toc::[]

== Tips for learning Fulcro

Read the docstrings of Fulcro's functions and vars. They are typically very good and insightful.

Don't be afraid to read the code of Fulcro itself. Even if you won't understand everything, you will gleam valuable insights. It is OK to only skim over and focus on the parts you can understand. Have a look at `defsc` and `defrouter` and the underlying functions, `transact!` and `load!` etc. You will be better for it.

== Troubleshooting Fulcro

It is essential for your productivity that you get good at troubleshooting Fulcro, so that you can detect why something does not work as expected. A key part of that is understanding Fulcro enough to know where to "zoom in" and leveraging Fulcro Inspect as much as possible - especially DB, DB Explorer, Transactions, and Network. I'd claim that:

> In Fulcro, the UI is a true function of data and in 90% cases you only need to look at the data and their connections - and perhaps transactions - to understand what is wrong. In 90% of these cases that beginners encounter, the problem is a broken connection between data^1^. 
>
> ^1^) Examples of breaking connections: Not including `get-query` in the parent's query, not setting/propagating up initial state where it is necessary.

Hopefully you have got somewhat proficient with Fulcro Inspect when doing the https://github.com/fulcro-community/fulcro-exercises[Fulcro exercises]. Remember to play with Fulcro Inspect and dig around your application with it to really understand the app while it is working well and as you are changing it so your skills will be sharp when you need them. The https://www.youtube.com/playlist?list=PLYvJiiE4TAijBAvO-R0PO8plSto4wtoQu[Learning Fulcro - troubleshooting demos] - a series of short screencasts demonstrating how to troubleshoot various problems in a Fulcro app - can help you.

Finally, remember to consult the https://blog.jakubholy.net/2020/troubleshooting-fulcro/[Fulcro Troubleshooting Decision Tree], which guides you to get from a problem to the most appropriate troubleshooting steps. I would also highly recommend that you use my library https://github.com/holyjak/fulcro-troubleshooting[fulcro-troubleshooting] to receive feedback about possible Fulcro problems early and visibly. Also watch out for Fulcro warnings in the browser Console.

TIP: Remember that only rarely should you be clicking around your UI and observing its changes. In Fulcro, virtually all user actions can be triggered from the REPL using `transact!`. It is also possible to observe and manipulate app data directly with the same functions that Fulcro uses - for example to simulate turning a query into the props tree via `db->tree`. It is also easier to observe effects on the data in the client DB than in the UI itself. Use these tools instead of the UI - they are more more efficient and allow you to zoom in on the problem much more precisely.

== What to do when UI Components and Data Entities do not match 1:1

In the simplest case, each UI Component such as Player and Team corresponds to a data entity (player and team, respectively). But often that is not the case. What if I want to wrap Player in a CoolAnimationThingy, which has no data entity? What if I want to show only a PlayerSummary inside the Team and only display PlayerDetails in a popup after the summary is clicked? Here both the components display different views of the same data entity.

The article https://blog.jakubholy.net/2020/fulcro-divergent-ui-data/[Fulcro Explained: When UI Components and Data Entities Diverge] explains this well, and you should go and read it now. A brief summary follows:

A UI-only ("stateless") component, such as the CoolAnimationThingy, with no query of its own:: 
Solution: The nearest stateful (with a query) ancestor component (Team in our example) becomes the true parent of any stateful children of the stateless component, composes their query and initial state, creates them and passes them to the stateless component as its children.
A Data-only component (a.k.a. a query component)::
This is mostly only necessary when describing to Fulcro the data returned by a mutation, if it does not match an existing component. Solution (since Fulcro 3.5): link:{url-cljdoc-fulcro}com.fulcrologic.fulcro.raw.components#nc[use `rc/nc`]; ex.: `(com.fulcrologic.fulcro.raw.components/nc [:user/id :user/email])`.
Multiple UI views of a single Data Entity::
This the case of the PlayerSummary and PlayerDetails and it is simple: they both use the same `:ident` (here, `:player/id`) and query for whatever they need.
A Data Entity spread across multiple sibling components::
Imagine you have a large data entity with many attributes. Displaying it in a single component would make it huge and hard to understand so you want to split the UI into multiple components, each displaying only a part of the entity. It differs from the "multiple views" by that they are all children of the <Entity> component and you want to load all the data at once. The solution is actually in Pathom: for each of these "virtual" children, include their query in the actual entity component as usual but under a "made-up" join key starting with `:>/` (the habitual https://blog.wsscode.com/pathom/v2/pathom/2.2.0/core/placeholders.html[Pathom "placeholder"] ns). The query `[.. {:>/dummy [:person/id :person/name]} ..]` is effectively the same as ``[.. :person/id :person/name ..]`.
Accessing top-level data from a nested component::
Sometimes you have a piece of data that you need at multiple places in the UI tree, loaded at the top of the client DB, for example `:current-user` (to display her name in the top bar and to only show the buttons allowed by her permissions everywhere). The simple solution is link:{url-book}#_link_queries[link query]: instead of including in your query `[... :current-user ...]` (which would only work in the root component), you would include `+[... [:current-user '_] ...]+`, which is an ident with the magical value `pass:[_]` meaning "give me the property's whole value as-is". 
In more extreme cases, you might need to split your UI into multiple independent parts, using Fulcro's multiple-roots-renderer.
Sharing data between diverse components on the page::
If you have a piece of data that multiple components, at different parts of the UI tree, need to access then the simplest solution is to put the data or a reference to the data into the root of the client DB and to use Link Query to access it from those components.
Inserting a stateful UI component between a parent-child entities::
This section essentially explains why inserting a router component into your UI does not break the query. You should not need to do this yourself but it will help you understand routers.

== Routing

In a single page application, you want to enable the user to navigate to parts of the application and to only display these "active" parts. You might also want to only load the necessary data when you are are about to display the relevant part of the UI. Often you also want to bind the route to the URL. All this is the task of routing.

Fulcro's link:{url-book}#_dynamic_router[Dynamic Routing] enables you to:

1. Make Routers that have multiple target components and only display the active one (i.e. the one you have routed to)
2. The routers can be nested, e.g. to display a particular "page" and then a particular "tab" within it
3. Bind the route to the URL (Fulcro's routing is fully independent of the URL but the two can be hooked together)
4. Load the data just before the component is about to be displayed
5. The target component can dynamically decide whether to allow the user to route to it or to route out of it (e.g. you do not want to allow the user to leave a half-filled form until she either saves it or cancels the edit)
6. Split the code so that parts of it will only be loaded when the part of the application they define is going to be displayed
7. Only query for the props of its current target from the client DB, which is important for the performance of bigger apps
8. Display a loading indicator after a short delay and to mark the route as failed if it does not complete within a time limit

=== The bare essentials of Fulcro routing

To add routing to your application, you need to:

1. Create a router using `dr/defrouter` and give it the list of the target components, only one of which will be displayed at a time. Notice that there is no magic in `defrouter` - it just produces an old good `defsc`
2. Give each target component a unique _route segment_ so that you can route to this target. You can think of the route segment as a relative URL. It starts with a non-empty literal string and may contain other strings and placeholders (keywords) for capturing dynamic _route parameters_ such as IDs. They do compose, if you have multiple levels of nested routers
.. The component can use the `:will-enter` component option function to access the route parameters (which are always strings) and to translate them into the actual ident via `(dr/route-immediate <ident>)` or `route-deferred`
3. Initialize everything and then route to the target you want displayed via `dr/change-route`

Let's explore a minimalist example of routing. This is the UI that we are creating:

image::bare-essentials-routing-ui-mockup.png["A mock up of a UI with a router (via wiredjs.com)"]

image::../images/bare-essentials-routing-ui-mockup.png["FIXME: remove"]

+++<!--FIXME: Remove the relative path img above-->+++

We show either all the people or just a single person. This is the code:

.Bare essentials of Fulcro routing (1st half - router & targets)
====
```clojure
(defsc AllPeople [_ {:keys [all-people]}]
  {:ident (fn [] [:component/id ::AllPeople])
   :query [{:all-people [:person/id :person/name]}]  ; <1>
   :initial-state {}                                 ; <1>
   :route-segment ["all"]}                           ; <2>
   (dom/div
     (dom/h3 "All People")
     (dom/ul
       (mapv (fn [{:person/keys [id name]}] (dom/li {:key id} name))
         all-people))))

(defsc Person [_ {:person/keys [id name biography]}]
  {:ident :person/id
   :query [:person/id :person/name :person/biography]  ; <1>
   :initial-state {}                                   ; <1>
   :route-segment ["person" :person-id]                ; <3>
   :will-enter (fn [app route-params]                  ; <4>
                 (dr/route-immediate
                   [:person/id
                    (js/parseInt (:person-id route-params))]))}
  (dom/p (str "Person #" id ": ") (dom/strong name) " - " biography))

(dr/defrouter MyRouter [_ _] {:router-targets [AllPeople Person]}) ; <5>
```
====
<1> A router target _must always_ have a query (use the "give me everything" query `['*]` if it doesn't need any) and a non-nil initial state (so at least an empty `{}`)
<2> A target's `:route-segment` assigns it a routing path (relative to an ancestor target, if any)
<3> The `:route-segment` may also contain placeholders to capture parameters from the route (here `:person-id`),
    which can then be accessed in `:will-enter` under that keyword
<4> A target may use the `:will-enter` component option function to access route params and to tell Fulcro which ident
    to route to (here via `dr/route-immediate`)
<5> `defrouter` creates the router component. It needs at least the list of its targets in `:router-targets`

WARNING: will-enter can be called multiple times as part of the route resolution algorithm and MUST NOT side-effect 

.Bare essentials of Fulcro routing (2nd half - Root and init)
====
```clojure
(def ui-my-router (comp/factory MyRouter))

(defsc Root [this {:ui/keys [router]}]
  {:query [{:ui/router (comp/get-query MyRouter)}]           ; <1>
   :initial-state {:ui/router {}}}                           ; <2>
  (dom/div
    (dom/p (dom/button {:onClick #(dr/change-route! this ["all"])} "All") ; <3>
           (dom/button {:onClick #(dr/change-route! this ["person" "123"])} "Person 123"))
    (ui-my-router router)))

(defn init [app]
  ;; Avoid startup async timing issues by pre-initializing things before mount
  (app/set-root! app Root {:initialize-state? true})
  (dr/initialize! app)                                         ; <4>
  (run! #(merge/merge-component! app Person %
           :append (conj (comp/get-ident AllPeople {}) :all-people))
    [#:person{:id 100 :name "Kamƒça" :biography "..."}
     #:person{:id 123 :name "Doubravka" :biography "..."}])
  (dr/change-route! app ["person" "123"])                       ; <5>
  ;; Note: üëÜüèø will warn https://book.fulcrologic.com/#warn-uism-sm-not-in-state; ignore! <7>
  ;; or: (dr/change-route! app (dr/path-to Person "123"))           ; <6>
  ;; or: (dr/change-route! app (dr/path-to Person {:person-id "123"})) ; <6>
  (app/mount! app Root "app" {:initialize-state? false}))
```
====
<1> You _must_ compose the router's query into its parent, under an arbitrary name (the `:ui/` ns is practical so that we do not unnecessarily include it in queries sent to the server)
<2> You _must_ compose the router's initial state into its parent (notice we use the https://book.fulcrologic.com/#_template_mode[template mode of initial state] so the `{}` actually means "include the router's initial state")
<3> We use `dr/change-route!` with the absolute route (concatenation of all route segments along the way from Root) to display the desired component
<4> When we start the application, we need to initialize the dynamic routing subsystem with `dr/initialize!`
<5> It is highly advisable to also make sure each router in the app is "routed" and thus in a well-defined state before it is ever displayed. Here we achieve that by routing to it in the init function. This can only be done after the application's state is initialized, i.e. either `app/set-root!` with `{:initialize-state? true}` or `app/mount!`
    * link:{url-book}#_setting_the_route_early[The Fulcro book advises]: "`An even better approach is to use state to block rendering until such time as a route or load is ready just by looking at the current state of your top-most router‚Äôs state machine.`" - see the example code below the quote
<6> Alternatively, instead of hard-coding the route as a vector of strings, we can use `(path-to & targets-and-params)` with component names to get the route - which is easier to navigate and to refactor. In the map form, there is just a single map with all the route's params at the very end.
<7> Note: As of Fulcro 3.5.6, you are likely to get the https://book.fulcrologic.com/#warn-uism-sm-not-in-state[Attempt to get an ASM path...] error for the router because `dr/initialize!` is asynchronous and does not finish before the `dr/change-route!` call. You can safely ignore it.

See the https://github.com/holyjak/minimalist-fulcro-template-backendless/blob/example/bare-essentials-routing/src/com/example/ui.cljs[full code here].

=== Binding the route to the URL

Reflecting the current route in the URL and changing the URL according to the route is simple. We just need to hook into the HTML 5 History API to call `change-route!` when the URL changes and to create our own routing function that also updates the URL before calling `change-route!`. Mapping from the URL to the route and vice versa just requires replacing `/` with spaces and vice versa. You can look at Fulcro RAD's `routing/html5-history` for inspiration, namely at link:{url-code-rad}routing/html5_history.cljc#L96[how it updates the URL via `.pushState`] and how it link:{url-code-rad}routing/html5_history.cljc#L143[uses `.addEventListener`] for the https://developer.mozilla.org/en-US/docs/Web/API/Window/popstate_event[`"popstate"` event] to get notified of URL changes.

The simplest solution is to include https://github.com/fulcrologic/fulcro-rad[fulcro-rad] as a library and to use its `com.fulcrologic.rad.routing/route-to!` instead of `change-route!`. You also need to link:{url-code-rad-demo}src/shared/com/example/client.cljs#L58[install-route-history! during app initialization] and perhaps also {url-code-rad-demo}/src/shared/com/example/client.cljs#L32[set the route at startup] (and not only when it is changed thereafter). Remember that RAD is just an addon library to Fulcro and you can choose and pick whatever parts you want.

If you do not want to include RAD or if you like to learn via video, have a look at https://youtu.be/oQpmKWBm9HE?t=867[Fulcro ‚Äì Part 15: Sessions and UI Routing] that shows how to do the integration manually (though with a helper history library). You may also find useful https://github.com/aeberts/fulcro-notes-public/blob/master/fulcro-videos-pt15-sessions-and-routing.org[these time-stamped notes from the video] by Alex Eberts.

NOTE: To make sure the user can reload the URL from the server, make sure that any path other than images, JS etc. falls back to the application's HTML page - https://github.com/fulcrologic/fulcro-rad-demo/blob/develop/src/shared/com/example/components/ring_middleware.clj#L48[see how fulcro-rad-demo does it].

=== Loading data when a component is routed to (for the first time)

Often we only want to load data when it is actually necessary, i.e. when the component that needs them is being displayed. In React it is achieved via the `componentWillMount` hook. In Fulcro we have a better solution if the component is a router target - namely its `:will-enter` component option function. (A key disadvantage of componentWillMount is "cascading loads", when a component A loads its data and renders its child B, triggering B's own load etc. In Fulcro we route to a leaf target and thus it and all intermediaries can start loading data in parallel.) Let's see how it looks.

.Loading data when a target is routed to
====
```clojure
(defsc Person [_ props]
  {:ident :person/id
   :query [:person/id :person/name]
   :initial-state {}
   :route-segment ["person" :person-id]
   :will-enter (fn [app route-params]    ; <1>                 
                 (let [ident [:person/id (-> route-params :person-id js/parseInt)]] ; <2>
                   (if (get-in (app/current-state app) ident)) ; <3>
                     (dr/immediate ident) 
                     (dr/route-deferred    ; <4>
                       ident
                       #(df/load! app ident Person
                         {:post-mutation `dr/target-ready
                          :post-mutation-params {:target ident}}))))}
  ...)
```
<1> Use `:will-enter`, which is called when it is decided to route to the target (and must return `route-immediate` or `route-deferred`). Beware that it may be called multiple times and must not side-effect.
<2> Construct the ident of the component that will be displayed, leveraging the provided route-params
    (as defined by the `:route-segment`)
<3> If the data has already been loaded previously then route to the component immediately. Otherwise load it first.
<4> Return `route-deferred` with the ident and a completion function that will be executed shortly by Fulcro, and which needs to eventually trigger the `dr/target-ready` post-mutation to tell Fulcro that the component is ready. Here the completion function does load the data and uses ``load!``'s `:post-mutation` and `:post-mutation-params` to make sure that `target-ready` is transacted after the load.
====

This is also called link:{url-book}#_deferred_routing[deferred routing and you can read more] about it in the Fulcro book. Here is a https://github.com/holyjak/minimalist-fulcro-template-backendless/blob/example/deferred-routing/src/com/example/ui.cljs#L30[complete, working example you can play with].

==== On router timeouts and errors

What happens when loading data in a deferred route takes too long? What if it fails? The router has you covered:

.Router with timeouts, a loading indicator, and an error message
====
```clojure
(dr/defrouter MyRouter [_ {:keys [current-state route-factory route-props]}]
  {:router-targets [AllPeople Person]}
  ;; The body of the router is displayed only when the target is not ready,
  ;; i.e. in one of the states below (unless you set `:always-render-body?`)
  (case current-state ; <1>
    nil (println "MISTAKE: MyRouter is displayed but has never been routed to yet") ; <2>
    :pending (dom/div "Loading...") ; <3>
    :failed (dom/div "Failed!")     ; <4>
    (println "Should never come here:" current-state))) ; <5>
```
<1> The body of the router is only displayed when the target is not ready (unless you set `:always-render-body? true`). Thus the only router states we will ever see here are those listed below.
<2> `current-state = nil` is the initial state, before the router has been routed to - i.e. when you failed to initialize it properly via `change-route!`, as described above (there is also the `:inital` state but I have not seen it in practice)
<3> `:pending` is active for a deferred route, after the `:deferred-timeout` passed but before the `:error-timeout` - it also ends if `dr/target-ready` is transacted for the target ident
<4> `:failed` if the deferred route has not become ready (as indicated by `dr/target-ready`) within the `:error-timeout` since the route event
<5> We do not expect to see any other states, namely `:routed` - because in this state the target itself is displayed and not the router's body (unless we set `:always-render-body? true`)
====

Key points to know:

* There are two timeouts that control the router's behavior with respect to deferred routes, `:deferred-timeout` (default 20ms)
  and `:error-timeout` (default 5s). You can override the default values when calling `dr/change-route!`, link:{url-cljdoc-fulcro}com.fulcrologic.fulcro.routing.dynamic-routing#change-route![see its docstring].
* You can use the body of the router and its `current-state`. It is only displayed when the target is not "ready" to be displayed
* The router switches from displaying the previous target to displaying the router's body while waiting for a pending target only after the `:deferred-timeout` has passed (but it switches to the target immediately when it gets ready)
* If the mutation `(dr/target-ready {:target <ident>}` is not triggered within `:error-timeout` of `change-route!` then the state becomes `:failed` (the target can still become "ready" later, if the load eventually finishes, and the target will be displayed)
* Thus we can use `:pending` to display a loading indicator, if it takes longer than the `:deferred-timeout`. We can use the `:failed` state to show an error message telling the user it is either taking too long or failed

=== Nesting routers

Having a UI tree with multiple levels of nested routers like this:

....
Root
  PageRoute
    WelcomePage
    SportPage, route `["sport" :sport]`
      Teams
        Team
          TeamRouter
            TeamStats, route ["team-stats"]
            Player, route ["player" :player-id]
....

we can route to a leaf router including the full route:

```clojure
(dr/change-route! app-or-component ["sport" "hockey" "player" "123"])
;; We could also leverage path-to:
(dr/path-to app-or-component SportPage "hockey" Player "123")
; =>  ["sport" "hockey" "player" "123"]
(dr/path-to app-or-component SportPage Player {:sport "hockey", :player-id "123"})
; =>  ["sport" "hockey" "player" "123"]
```

Somewhere under a router target, we can also use relative routing:

```clojure
;; e.g. inside Teams:
(dr/change-route-relative! teams-this ["player" "123"])
```

We can even route from one target to another sibling target by prefixing the relative route with `:...`:

```clojure
;; Inside Player:
(dr/change-route-relative! player-this [:... "team-stats"])
```

IMPORTANT: 
It is essential that each router in the app is in a routed state before it is displayed. 
Always route to a leaf target! (Or do so at least once, e.g. during app initialization.)
(A router will display its first target by default but it is not the same as routing to it manually and some things _will not work_.) See link:{url-book}#_partial_routes[20.6.1. Partial Routes] for details.

=== A router inside a newly ``load!``ed component

Imagine you have the non-singleton component Person, with an ident like `[:person/id "<some-value>"]`. Inside Person, you want to show routable tabs with different groups of information about the person (job info, health history, family, ...). So you decide to use a router for this.

But there is a problem. It is likely the component's data wasn't part of the initial state of the application and only was load!-ed later. Thus it will not be "linked" properly to the router, i.e. its data in the client DB will be missing something like `:<the router prop you made up> [::dr/id :<YourRouterComponentName>]`. For components that exist at the application start, this link is established by including the router property in their initial state and composing it up to the Root, as we discussed above, but here you need to establish the link yourself. You add the "link" manually using link:{url-book}#_pre_merge[`:pre-merge`], which is a component option holding a function called by Fulcro before normalizing and merging incoming data. Pre-merge gets the loaded, denormalized data and returns the same denormalized data, with you custom modifications. See here:

```clojure
;; Somewhere in the app: `(df/load! app [:person/id 123] MyPerson)`
(defsc MyPerson [_ props]
  {:ident :person/id
   :query [:person/id :ui/person-router ...]
   :initial-state {:ui/person-router {}}                         ; <1>
   :pre-merge (fn [{:keys [loaded-data-tree current-state-map]}] ; <2>
                (merge (comp/get-initial-state MyPerson)         ; <3>
                  {:ui/person-router (get-in current-state-map (comp/get-ident PersonRouter {}))} ; <4>
                  loaded-data-tree))}
  (ui-person-router (:ui/person-router props)))
```
<1> The parent of a router must compose its initial state into its own (and so on all the way up to the Root)
<2> Pre-merge gets the (denormalized) tree of data from `df/load!` and the current value of the client DB (a.k.a. state map)
<3> We include the component's initial state, which composes the initial state of the router
<4> We make sure to preserve any state the router might already have (which we must denormalize)
// FIXME: Do I need both <3> and <4> ?! Isn't 4 enough? Also, at 4 we do not really denorm. the content of the router data?!

Read more in the Fulcro Book, link:{url-book}#_composing_the_routers_state_into_the_parent[20.4. Composing the Router‚Äôs State into the Parent].


=== Routing-related component options

We have already seen `:will-enter` but link:{url-book}#_routing_targets[there are also other optional component options] you might want to know about: `:route-cancelled`, `:will-leave`, `:allow-route-change?`, `:route-denied`.

.Deep dive: how do routers work?
****
Routers use _dynamic queries_ (discussed later on) to set the sub-query of their `::dr/current-route` to be the active target's one. When they render the target component, they pass it `(::dr/current-route props)`, together with any computed props.

You can learn more about it in https://blog.jakubholy.net/2020/fulcro-divergent-ui-data/#_inserting_a_stateful_ui_component_between_a_parent_child_entities[When UI and Entities Diverge: Inserting a stateful UI component between a parent-child entities].
****

== Creating and editing data entities

=== Tempids and creating new entities

=== Form state management

== Etc... (UISM, dynamic.recursive/union queries, react interop, ...)